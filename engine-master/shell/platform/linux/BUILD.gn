# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_linux)

import("$flutter_root/shell/platform/glfw/config.gni")

group("linux") {
  if (build_glfw_shell) {
    deps = [
      ":flutter_linux_glfw",
      "$flutter_root/shell/platform/glfw:publish_headers_glfw",
      "$flutter_root/shell/platform/glfw/client_wrapper:publish_wrapper_glfw",
    ]
  }
}

# Temporary workraround for the issue describe in
# https://github.com/flutter/flutter/issues/14509 and
# https://github.com/flutter/flutter/issues/14438
# Remove once the build infrastructure moves to Ubuntu 18.04 or newer, where
# the underlying issue is fixed.
config("disable_fatal_link_warnings") {
  visibility = [ ":*" ]
  ldflags = [ "-Wl,--no-fatal-warnings" ]
}

if (build_glfw_shell) {
  shared_library("flutter_linux_glfw") {
    deps = [
      "$flutter_root/shell/platform/glfw:flutter_glfw",
    ]

    configs += [ ":disable_fatal_link_warnings" ]

    public_configs = [ "$flutter_root:config" ]
  }
}
